plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.12' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'jacoco'
    id "org.asciidoctor.jvm.convert" version "3.3.2" apply false
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

def commonSubProjects = [
        project(":point-core"),
        project(":point-api"),
        project(":point-worker"),
        project(":point-batch"),
        project(":clients"),
]

configure(commonSubProjects) {
    apply plugin: "java"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "jacoco"

    group = "${projectGroup}"
    version = "${projectVersion}-${new Date().format('yyyyMMddHHmm')}"

    sourceCompatibility = 17
    targetCompatibility = 17

    dependencies {
        compileOnly("com.google.code.findbugs:jsr305:3.0.2")
        compileOnly("org.projectlombok:lombok")
        annotationProcessor("org.projectlombok:lombok")

        implementation("org.springframework.boot:spring-boot-starter-logging")

        testImplementation("org.springframework.boot:spring-boot-starter-test")
        testImplementation(testFixtures(project(":point-core")))
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:2021.0.3")
            mavenBom("io.awspring.cloud:spring-cloud-aws-dependencies:2.4.2")
            mavenBom("com.amazonaws:aws-java-sdk-bom:1.12.262")
        }
    }

    // jacoco
    jacoco {
        toolVersion = "0.8.8"
    }

    // test
    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
        jacoco {
            enabled = true
            destinationFile = file("${buildDir}/jacoco/jacoco.exec")
        }
        useJUnitPlatform()

        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
        }
    }
}
